cmake_minimum_required(VERSION 3.26)
project(app)


find_package(SDL3 REQUIRED)

include(FetchContent)

FetchContent_Declare(
    glad
    GIT_REPOSITORY https://github.com/Dav1dde/glad
    GIT_TAG        v2.0.6
    GIT_SHALLOW    TRUE
    GIT_PROGRESS   TRUE
)

FetchContent_GetProperties(glad)
if(NOT glad_POPULATED)
    message("Fetching glad")
    FetchContent_MakeAvailable(glad)

    add_subdirectory("${glad_SOURCE_DIR}/cmake" glad_cmake)
    glad_add_library(glad 
        REPRODUCIBLE 
        EXCLUDE_FROM_ALL 
        LOADER 
        API gl:core=3.3 
        EXTENSIONS 
            GL_ARB_bindless_texture 
            GL_EXT_texture_compression_s3tc
    )
endif()

FetchContent_Declare(
    imgui
    GIT_REPOSITORY https://github.com/ocornut/imgui
    GIT_TAG v1.92.5-docking
    GIT_SHALLOW    TRUE
    GIT_PROGRESS   TRUE
)

FetchContent_GetProperties(imgui)
if(NOT imgui_POPULATED)
    message("Fetching ImGui")
    FetchContent_MakeAvailable(imgui)
    
    add_library(imgui STATIC
        ${imgui_SOURCE_DIR}/imgui.cpp
        ${imgui_SOURCE_DIR}/imgui_draw.cpp
        ${imgui_SOURCE_DIR}/imgui_tables.cpp
        ${imgui_SOURCE_DIR}/imgui_widgets.cpp
        ${imgui_SOURCE_DIR}/backends/imgui_impl_sdl3.cpp
        ${imgui_SOURCE_DIR}/backends/imgui_impl_opengl3.cpp
    )

    target_link_libraries(imgui PUBLIC SDL3::SDL3)

    target_include_directories(imgui PUBLIC 
        ${imgui_SOURCE_DIR}
        ${imgui_SOURCE_DIR}/backends
    )
endif()

add_executable(main)

target_sources(main
PRIVATE
    main.cpp
)

target_link_libraries(main 
    PRIVATE 
        SDL3::SDL3
        opengl32
        imgui
        glad
)

if(MINGW)
	# This removes the need for libgcc_s_seh-1.dll and libstdc++-6.dll
	target_link_options(main PRIVATE -static-libgcc -static-libstdc++ -static)
    add_custom_command(
        TARGET main POST_BUILD
        COMMAND "${CMAKE_COMMAND}" -E copy $<TARGET_FILE:SDL3::SDL3-shared> $<TARGET_FILE_DIR:main>
        VERBATIM
    )
endif()

install(TARGETS main DESTINATION .)
install(FILES 
    "${CMAKE_CURRENT_BINARY_DIR}/SDL3.dll"
    DESTINATION .
)